plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.nurazlib.custompopupmodern'
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.nurazlib.lib'
                artifactId = 'custompopupmodern'
                version = '1.0.0.alpha23'

                pom {
                    name = 'Custom Popup Modern'
                    description = 'A custom modern popup library for Android'
                    url = 'https://github.com/KnosTx/CustomPopupModern'
                    
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'nurazlib'
                            name = 'NurAzli'
                            email = 'nurazligaming@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/KnosTx/CustomPopupModern.git'
                        developerConnection = 'scm:git:ssh://github.com/KnosTx/CustomPopupModern.git'
                        url = 'https://github.com/KnosTx/CustomPopupModern'
                    }
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/KnosTx/CustomPopupModern") // Ganti dengan repo GitHub Anda

                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME_GITHUB")
                    password = project.findProperty("gpr.token") ?: System.getenv("TOKEN_GITHUB")
                }
            }
        }
    }
}
